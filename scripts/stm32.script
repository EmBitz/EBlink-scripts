//! STmicro
/////////////////////////////////////////////////////
//
//            STmicro cortex-M devices
//
//     The script tries to detect the right memory sizes
//     however, if this is not correct you can set the sizes
//     in kilobytes on the command line as:
//
//         -D FLASH_SIZE=xxx  and -D RAM_SIZE=xxxx
//
//     Fix chip_id for F4 rev A errata , Read CPU ID, as CoreID is the same for F2/F4 - 0x411
//     so we have 0x411 also as a cortex-M4 in the list (see patch).
//

// The supported device matrix
STMICRO <- [
["M0+", [["l0", [ 0x417, 0x425, 0x447, 0x457 ]],
         ["gx", [ 0x466, 0x456, 0x460, 0x467 ]],
         ["C0", [ 0x443, 0x453 ]]]],

["M0",  [["f0", [ 0x440, 0x442, 0x444, 0x445, 0x448 ]]]],

["M3",  [["f1", [ 0x410, 0x412, 0x414, 0x418, 0x420, 0x428, 0x430 ]],
         ["f2", [ 0x411 ]],
         ["l1", [ 0x416, 0x427, 0x429, 0x436, 0x437 ]]]],

["M4",  [["f4", [ 0x413, 0x419, 0x421, 0x423, 0x431, 0x433, 0x434, 0x441, 0x458, 0x411 /* <- patch*/ ]],
         ["f3", [ 0x422, 0x432, 0x438, 0x439, 0x446 ]],
         ["l4", [ 0x415, 0x435, 0x461, 0x462, 0x464, 0x470, 0x471 ]],
         ["gx", [ 0x468, 0x469, 0x479 ]],
         ["wx", [ 0x495, 0x496, 0x497 ]]]],

["M7",  [["f7", [ 0x449, 0x451, 0x452 ]],
         ["h7", [ 0x450, 0x480, 0x483 ]]]],

["M33", [["u5", [ 0x455, 0x476, 0x481, 0x482 ]],
         ["l5", [ 0x472 ]],
         ["h5", [ 0x474, 0x484 ]]]]
]

// The register scan list for the CPU ID
STMICRO_REG <- [
    0xE0042000,
    0x40015800,
    0x5C001000,
    0xE0044000,
    0x44024000
]

intrfApi <- InterfAPI()  // Global so that all included script files also have access
targetApi<- TargetAPI()  // Global interface to the target class
deviceId <- 0            // Global deviceId

// Used by scripts to denote a not found device
const ERROR_NOT_FOUND -100

/////////////////////////////////////////////////////
//
//  EBlink called Entry point
//
function main()
{
    try{
        findDevice()
    }
    catch(e){
        if( e == ERROR_NOT_FOUND)
            if(deviceId){
                printf("\n\nPlease report this ID so that we can add it.\n")
                errorf("Error unsupported STM32 ID: 0x%X\n", deviceId)
            }
            else
                errorf("ERROR: No valid STmicro device ID found\n")
        else
            errorf("Error:\tCan't access target device!\n\tIf reset is disabled (\"dr\" option), try with reset.")
        return ERROR_NOTIFIED
    }
    return ERROR_OK
}

/////////////////////////////////////////////////////
//
//
//
function findDevice()
{
    if( VERSION < 5.0){
       errorf("Error:\n\nIncompatiable EBlink version %.2f\n\tPlease update\n", VERSION)
       throw ERROR_NOTIFIED
    }

    foreach(cpuReg in STMICRO_REG)
        if( (intrfApi.readMem32(cpuReg)>=0) && intrfApi.value32){
            deviceId = intrfApi.value32 & 0xfff
            break
        }

    if(deviceId == 0)
        throw ERROR_NOT_FOUND

    noticef("STmicro device : 0x%X\n", deviceId)

    foreach(arch in STMICRO)
      if(arch[0] == targetApi.getCpuName()){
          foreach(script in arch[1])
              foreach(devId in script[1])
                  if( devId == deviceId){
                    noticef("STmicro script : %s\n", script[0])
                    require("stmicro/stm32"+script[0]+".script")
                    stm32_device()
                    return
                  }
          break
      }

    throw ERROR_NOT_FOUND
}

/////////////////////////////////////////////////////////////////////////////////
//
// Additional commands after reset (optional) called by EBlink
//
// resetType:
//        SYSTEM_RESET
//        CORE_RESET
//        JTAG_RESET
//        USER_RESET
//
function  reset_post(resetType)
{
    // Check if there is an user defined post reset hook, the user
    // can add his own reset strategy with -S <myscript> as last
    // defined script on the cli.
    if (isScriptObject("reset_post_hook"))
        return reset_post_hook(resetType);

    // We don't use this if the reset is a user_script type. In that
    // case, the user is responsible for the initialization.
    //
    if( resetType != USER_RESET ){
        // Set the Stack pointer according the Vector table entry
        intrfApi.readMem32(0x08000000)
        targetApi.writeReg("SP", intrfApi.value32)

        // Set the Program pointer according the Vector table entry
        intrfApi.readMem32(0x08000004)
        targetApi.writeReg("PC", intrfApi.value32)
    }

    return ERROR_OK
}

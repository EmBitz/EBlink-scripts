/////////////////////////////////////////////////////////////////////////////////
//
//                   STM32H5
//
//  ToDo:  - option programming
//         - HighCycle data programming
//
// Used parameters from parent script(s)
//
// - flashBank2Start
// - startHcSec
//

const FLASH_BASE 0x40022000

// Register offsets of the flash controller
const FLASH_ACCESS_CTRL (0x00)
const FLASH_KEYR        (0x04)
const FLASH_OPTION_KEY  (0x0c)
const FLASH_NSSR        (0x20)
const FLASH_NSCR        (0x28)
const FLASH_NSCCR       (0x30)

// FLASH_NSCR control bit flags
const FLASH_NSCR_LOCK         (1 << 0)
const FLASH_NSCR_PGM          (1 << 1)
const FLASH_NSCR_SER          (1 << 2)
const FLASH_NSCR_BER          (1 << 3)
const FLASH_NSCR_FW           (1 << 4)
const FLASH_NSCR_START        (1 << 5)
const FLASH_NSCR_MASS_MER     (1 << 15)
const FLASH_NSCR_EOPIE        (1 << 16)
const FLASH_NSCR_WRPERRIE     (1 << 17)
const FLASH_NSCR_PGSERRIE     (1 << 18)
const FLASH_NSCR_STRBERRIE    (1 << 19)
const FLASH_NSCR_INCERRIE     (1 << 20)
const FLASH_NSCR_OBKERRIE     (1 << 21)
const FLASH_NSCR_OBKWERRIE    (1 << 22)
const FLASH_NSCR_OPTCHGERRIE  (1 << 23)
const FLASH_NSCR_BKSEL        (1 << 31)
const FLASH_NSCR_PAGE_SHIFT   (6)

// Flash NSSR flags
const FLASH_NSSR_BUSY       (1 << 0)
const FLASH_NSSR_WBNE       (1 << 1)
const FLASH_NSSR_BUSY       (1 << 3)
const FLASH_NSSR_EOP        (1 << 16)
const FLASH_NSSR_WRPERR     (1 << 17)
const FLASH_NSSR_PGSERR     (1 << 18)
const FLASH_NSSR_STRBERR    (1 << 19)
const FLASH_NSSR_INCERR     (1 << 20)
const FLASH_NSSR_OBKERR     (1 << 21)
const FLASH_NSSR_OBKWERR    (1 << 22)
const FLASH_NSSR_OPTCHGERR  (1 << 23)

// FLASH_NSCCR  flags
const FLASH_NSCCR_CLREOP        (1 << 16)
const FLASH_NSCCR_CLRWRPERR     (1 << 17)
const FLASH_NSCCR_CLRPGSERR     (1 << 18)
const FLASH_NSCCR_CLRSTRBERR    (1 << 19)
const FLASH_NSCCR_CLRINCERR     (1 << 20)
const FLASH_NSCCR_CLROBKERR     (1 << 21)
const FLASH_NSCCR_CLROBKWERR    (1 << 22)
const FLASH_NSCCR_CLROPTCHGERR  (1 << 23)

// All the possible errors mask
const FLASH_NSSR_ERROR_MASK 0xFC0000

const FLASH_KEYR1  0x45670123
const FLASH_KEYR2  0xCDEF89AB

maxFlashSpeed   <- 24000 // Can be changed from CLI

// Generic Flash Script errors
require("stmicro/flash/errors.script")

/////////////////////////////////////////////////////////////////////////////////
//
// EBlink callback
//
//   Unlock the flash
//
function flash_start()
{
    // Override the maximum flash speed from cli
    if (isScriptObject("FLASH_SPEED") && FLASH_SPEED>0){
        maxFlashSpeed = FLASH_SPEED
        printf("Flash speed set: %d KHz\n", maxFlashSpeed)
    }

    // Check that we don't go faster than the user selected on the cli
    if(maxFlashSpeed > intrfApi.getSpeed())
        maxFlashSpeed = intrfApi.getSpeed()

    try{
        // Be sure that the core is halted
        _n_throw( targetApi.reset(SYSTEM_RESET) )

        // Wait for halt
        local time = GetTickCount()
        do{
            // Timeout of 1sec
            if(GetTickCount() - time > 1000)
                _n_throw(ERROR_TIMEOUT)

            _n_throw( targetApi.poll() )
        } while(targetApi.getState() != TARGET_HALTED )

        flash_unlock()
        return ERROR_OK
    }

    // Catch all the lock errors
    catch(e){
       if(e < ERROR_NOTIFIED)
           errorf("Error: target initializing! %s\n", flashError(e) )
       return ERROR_NOTIFIED
    }
}


/////////////////////////////////////////////////////////////////////////////////
//
// EBlink callback
//
//   Erase sequence according ST user manual
//   Addr -  >=0x0900C000      : HighCycle #2 secNumber = 8-sector (inverse sector count)
//   Addr -  >=0x09000000      : HighCycle #1 secNumber = 8-sector (inverse sector count)
//   Addr -  >=flashBank2Start : Flash bank 2 secNumber = sector
//   Addr -  >=0x08000000      : Flash bank 1 secNumber = sector
//
function flash_erase(sector, address)
{
    try{
        // If this is high cycle calculate the right sector number
        if(address >= 0x09000000)
            sector += startHcSec;

        local eraseFlags = (sector << FLASH_NSCR_PAGE_SHIFT ) | FLASH_NSCR_SER | FLASH_NSCR_START

        // Is this flash bank 1 or 2 ?
        if( (address >= flashBank2Start && address < 0x09000000 ) || address >= 0x0900C000 )
            eraseFlags  = eraseFlags | FLASH_NSCR_BKSEL

        _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_NSCR, eraseFlags ) )

        flash_check_busy(10000)
        return ERROR_OK
    }

    // Catch all the sector erase errors
    catch(e){
       if(e < ERROR_NOTIFIED)
           errorf("ERROR: erasing sector %d failed! %s\n", sector, flashError(e) )
       return ERROR_NOTIFIED
    }
}


/////////////////////////////////////////////////////////////////////////////////
//
// EBlink callback
//
//  We don't use a flash loader here. We just write the pages word by word.
//  Because every word needs to be transported by USB, we don't check the busy
//  flag in between because we are not that fast.
//
function flash_write(sector, address, buffer)
{
    try{
        // Handle highCycle must be flashed with multiplies of 2
        // Normal memory must be flashed with multiplies of 16
        buffer.trim(address >= 0x09000000 ? 2 : 16 )

        // Set the PG bit of the FLASH control register
        _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_NSCR,  FLASH_NSCR_PGM ) )

        // Adjust the probe speed to the maximum for direct write
        local probeSpeed = intrfApi.getSpeed()
        intrfApi.setSpeed(maxFlashSpeed)

        // Program the flash with 32 bit and highCycle with 16 bit width memory access
        _n_throw( intrfApi.writeBuf(address, buffer, (address >= 0x09000000 ? 16 :32) ) )

        // Restore probe speed
        intrfApi.setSpeed(probeSpeed)

        flash_check_busy(10000)

        return ERROR_OK
    }

    // Catch all the sector write errors
    catch(e){
       if(e < ERROR_NOTIFIED)
           errorf("ERROR: writing sector %d failed! %s\n", sector, flashError(e) )
       return ERROR_NOTIFIED
    }
}

/////////////////////////////////////////////////////////////////////////////////
//
// EBlink callback
//
function flash_done()
{
    try{
        _n_throw( intrfApi.readMem32(FLASH_BASE + FLASH_NSCR) )
        _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_NSCR, intrfApi.value32 | FLASH_NSCR_LOCK) )
        return ERROR_OK
    }
    catch(e) {
       if(e < ERROR_NOTIFIED)
           errorf("ERROR: locking device\n")
        return ERROR_NOTIFIED
    }
}

/////////////////////////////////////////////////////////////////////////////////
//
//   Lock the flash
//
function flash_check_busy(timeout)
{
    local time = GetTickCount()
    do{
        AnimateCursor()
        _n_throw( intrfApi.readMem32(FLASH_BASE + FLASH_NSSR) )

        if(GetTickCount() - time > timeout)
            _n_throw(ERROR_TIMEOUT)

    }while ( (intrfApi.value32 & FLASH_NSSR_BUSY) )// && !( intrfApi.value32 & FLASH_NSSR_EOP))

    AnimateDone()

    if( intrfApi.value32 & FLASH_NSSR_ERROR_MASK)
            _n_throw( ERROR_FLASH )

    _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_NSCCR,
                    intrfApi.value32 & (FLASH_NSSR_ERROR_MASK | FLASH_NSSR_EOP)  ) )
}


/////////////////////////////////////////////////////////////////////////////////
//
//   Unlock the flash
//
function flash_unlock()
{
    // Unlock Flash
    _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_KEYR, FLASH_KEYR1 ) )
    _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_KEYR, FLASH_KEYR2 ) )

    _n_throw( intrfApi.readMem32(FLASH_BASE + FLASH_NSCR) )
    if(intrfApi.value32 & FLASH_NSCR_LOCK )
       _n_throw(ERROR_UNLOCK)
}


/////////////////////////////////////////////////////////////////////////////////
//
//  Called by EBlink if chip erase is needed (e.g. command line flashing)
//
//   Erase the whole chip
//   - If this function is not defined, sector by sector erase is used by EBlink.
//   - This is an isolated function, flash_start and flash_done are not called by EBlink
//
function flash_erase_chip()
{
    try{
        printf("Flash chip erase ")

        _n_throw( targetApi.halt() )
        flash_unlock()
        _n_throw( intrfApi.writeMem32(FLASH_BASE + FLASH_NSCR, FLASH_NSCR_MASS_MER |  FLASH_NSCR_START ) )
        flash_check_busy(10000)
        Sleep(500) // We need some back-off time here
        flash_done()
        printf("done\n")
        return ERROR_OK
    }

    // Catch all the sector write errors
    catch(e){
        if(e < ERROR_NOTIFIED)
           errorf("ERROR: mass erase failed! %s\n", flashError(e) )
        return ERROR_NOTIFIED
    }
}


